plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.diffplug.spotless' version '6.25.0'
}

group 'net.smoofyuniverse'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

configurations {
    spongevanilla
    spongeforge
}

dependencies {
    api 'org.spongepowered:spongeapi:10.0.0'

    implementation 'net.smoofyuniverse:oreupdater:1.2.0'
    implementation 'net.smoofyuniverse:worldmap:1.2.0'

    spongevanilla('org.spongepowered:spongevanilla:1.19.4-10.0.0-RC1552:universal') { transitive = false }
    spongeforge('org.spongepowered:spongeforge:1.19.4-45.1.19-10.0.0-RC1552:universal') { transitive = false }
}

jar {
    archiveClassifier = 'base'
}

shadowJar {
    archiveClassifier = ''

    dependencies {
        include dependency('net.smoofyuniverse:oreapi')
        include dependency('net.smoofyuniverse:oreupdater')
        include dependency('net.smoofyuniverse:worldmap')
    }

    relocate 'net.smoofyuniverse.ore', 'net.smoofyuniverse.autopickup.ore'
    relocate 'net.smoofyuniverse.map', 'net.smoofyuniverse.autopickup.map'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "utf-8"
}

tasks.register('setupVanillaServer', Copy) {
    into 'run/vanilla'

    from configurations.spongevanilla
    rename('spongevanilla-(.*).jar', 'spongevanilla.jar')

    into('mods') {
        from shadowJar
    }
}

tasks.register('setupForgeServer', Copy) {
    into 'run/forge/mods'

    from configurations.spongeforge
    rename('spongeforge-(.*).jar', 'spongeforge.jar')

    from shadowJar
}

spotless {
    ratchetFrom 'origin/master'

    java {
        licenseHeaderFile(rootProject.file('HEADER'))
    }
}
