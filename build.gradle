plugins {
    id 'java-library'
    id 'com.gradleup.shadow' version '8.3.3'
    id 'com.diffplug.spotless' version '6.25.0'
}

group 'net.smoofyuniverse'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

configurations {
    spongevanilla
    spongeforge
    spongeneo
}

dependencies {
    api 'org.spongepowered:spongeapi:15.0.0-SNAPSHOT'

    implementation 'net.smoofyuniverse:oreupdater:1.6.0'
    implementation 'net.smoofyuniverse:worldmap:1.3.0'

    spongevanilla('org.spongepowered:spongevanilla:1.21.5-15.0.0-RC2377:universal') { transitive = false }
    spongeforge('org.spongepowered:spongeforge:1.21.5-55.0.22-15.0.0-RC2377:universal') { transitive = false }
    spongeneo('org.spongepowered:spongeneo:1.21.5-21.5.75-15.0.0-RC2377:universal') { transitive = false }
}

jar {
    archiveClassifier = 'base'
}

shadowJar {
    archiveClassifier = ''

    dependencies {
        include dependency('net.smoofyuniverse:oreapi')
        include dependency('net.smoofyuniverse:oreupdater')
        include dependency('net.smoofyuniverse:worldmap')
    }

    relocate 'net.smoofyuniverse.ore', 'net.smoofyuniverse.autopickup.ore'
    relocate 'net.smoofyuniverse.map', 'net.smoofyuniverse.autopickup.map'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "utf-8"
}

tasks.register('setupVanillaServer', Copy) {
    into 'run/vanilla'

    from configurations.spongevanilla
    rename('spongevanilla-(.*).jar', 'spongevanilla.jar')

    into('mods') {
        from shadowJar
    }
}

tasks.register('setupForgeServer', Copy) {
    into 'run/forge/mods'

    from configurations.spongeforge
    rename('spongeforge-(.*).jar', 'spongeforge.jar')

    from shadowJar
}

tasks.register('setupNeoServer', Copy) {
    into 'run/neoforge/mods'

    from configurations.spongeneo
    rename('spongeneo-(.*).jar', 'spongeneo.jar')

    from shadowJar
}

spotless {
    ratchetFrom 'origin/master'

    java {
        licenseHeaderFile(rootProject.file('HEADER'))
    }
}
